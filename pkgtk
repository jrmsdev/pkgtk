#!/usr/bin/env tclsh8.6

package require Tk

#
# view local (installed) packages
#
proc view_pkglocal {w} {
    ttk::frame $w
    grid rowconfigure $w 0 -weight 1
    grid rowconfigure $w 1 -weight 6
    grid rowconfigure $w 2 -weight 3
    grid columnconfigure $w 0 -weight 1
    grid $w -sticky nwse
    ttk::label $w.info
    grid $w.info -row 0 -column 0 -sticky w
    ttk::treeview $w.pkgtree -show tree -selectmode browse
    grid $w.pkgtree -row 1 -column 0 -sticky nwse
    tkbusy_hold .
    set pkglist [pkglist_local]
    set llen [llength $pkglist]
    $w.info configure -text "Installed packages: $llen"
    set cur_section {}
    for {set i 0} {$i < $llen} {incr i} {
        set origin [lindex $pkglist $i]
        set pkg_section [lindex [split $origin /] 0]
        set pkg_name [lindex [split $origin /] 1]
        puts "$origin $pkg_section $pkg_name"
        if {[string compare "$pkg_section" "$cur_section"] != 0} {
            set sid [$w.pkgtree insert {} end -id $pkg_section -text $pkg_section]
            set cur_section $pkg_section
        }
        $w.pkgtree insert $cur_section end -id $origin -text $pkg_name
    }
    tkbusy_forget .
}

#
# dispatch view
#
proc dispatch_view {name} {
    set w .view
    if [winfo exists $w] {
        destroy $w
    }
    $name $w
}

#
# show error message
#
proc show_error {msg} {
    tk_messageBox -parent . -title "pkgtk error" -message "$msg" -type ok
}

#
# tk busy hold
#
proc tkbusy_hold {w} {
    tk busy hold $w
    tk busy configure $w -cursor watch
    update
}

#
# tk busy forget
#
proc tkbusy_forget {w} {
    tk busy forget $w
    update
}

#
# main menu
#
proc main_menu {} {
    menu .menu
    . configure -menu .menu
    .menu add command -label Quit -underline 0 -command {quit 0}
}

#
# pkg list local (installed) packages
#
proc pkglist_local {} {
    try {
        return [split [exec pkg query -e {%a == 0} %o | sort]]
    } trap CHILDSTATUS {results options} {
        show_error [dict get $options -errorinfo]
    }
}

#
# pkg list remote (available) packages
#
proc pkglist_remote {} {
    try {
        return [split [exec pkg rquery -a %o | sort]]
    } trap CHILDSTATUS {results options} {
        show_error [dict get $options -errorinfo]
    }
}

#
# exit main loop
#
proc quit {rc} {
    destroy .
    exit $rc
}

#
# sleep func
#
proc sleep {t} {
    after [expr round($t * 1000)]
}

#
# main
#
proc main {} {
    wm title . pkgtk
    wm minsize . 800 600
    grid rowconfigure . 0 -weight 1
    grid columnconfigure . 0 -weight 1
    main_menu
    dispatch_view view_pkglocal
}

main
